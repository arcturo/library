<div class="back"><a href="index.html">&laquo; 返回目录</a></div>

#CoffeeScript的自动编译

使用CoffeeScript的一个问题是，它会在你和JavaScript之间多加了一个层，当CoffeeScript文件频繁变更时你还需要手工编译它们。

如第一章所说，我们可以使用`coffee`命令来编译CoffeeScript文件：
    
    coffee --compile --output lib src
    
在上面的例子中，事实上，在`src`中的`.coffee`文件会被编译为JavaScript并且输出到`lib`目录中。这样编译看起来有点无聊，因此让我们看一下如何自动地编译。

##Cake

[Cake](http://jashkenas.github.com/coffee-script/#cake)是一个超级简单的与 [Make](http://www.gnu.org/software/make/)和 [Rake](http://rake.rubyforge.org/)类似的构建工具。该库被捆绑在`coffee-script`的npm安装包中，可以通过名为`cake`的可执行程序来使用。

你可以使用在一个名为`Cakefile`的文件中使用CoffeeScript来定义任务。Cake可以去读它，而且可以在当前目录中通过运行`cake[task][options]`来调用它。单单输入`cake`可以显示一个任务项以及参数的列表。

可以使用`task()`函数来定义任务项，给它传递一个名字、参数以及回调函数即可。例如，创建一个名为`Cakefile`的文件，还有两个目录——`lib`和`src`。把下面的代码添加到Cake文件中；

<span class="csscript"></span>

    fs = require 'fs'

    {print} = require 'sys'
    {spawn} = require 'child_process'

    build = (callback) ->
      coffee = spawn 'coffee', ['-c', '-o', 'lib', 'src']
      coffee.stderr.on 'data', (data) ->
        process.stderr.write data.toString()
      coffee.stdout.on 'data', (data) ->
        print data.toString()
      coffee.on 'exit', (code) ->
        callback?() if code is 0
    
    task 'build', 'Build lib/ from src/', ->
      build()
      
在上面的例子中，我们定义了一个名为`build`的任务项，该任务项可以通过运行`cake build`来调用。这运行的是与之前例子一样的命令。将`src`内的CoffeeScript文件编译为JavaScript放到`lib`中。你现在可以如往常一样在HTML中引用`lib`中的JavaScript文件了。

<span class="csscript"></span>

    <!DOCTYPE html>
    <html>
    <head>
    <meta charset=utf-8>
    <script src="lib/app.js" type="text/javascript" charset="utf-8"></script>      
    </head>
    <body>
    </body>
    </html>

我们在CoffeeScript代码变了之后还需要手动的运行`cake build`命令，这很不理想。幸运的是，`coffee`命令接受另外一个`--watch`参数，指示它监视一个目录的变化并且按需重新编译。让我们用它来定义另外一个任务项：

<span class="csscript"></span>

     task 'watch', 'Watch src/ for changes', ->
        coffee = spawn 'coffee', ['-w', '-c', '-o', 'lib', 'src']
        coffee.stderr.on 'data', (data) ->
          process.stderr.write data.toString()
        coffee.stdout.on 'data', (data) ->
          print data.toString()

如果一个任务项依赖了另一个，你是使用`invoke(name)`来运行其他任务项。让我添加一个实用懂得任务项到我们的`Cakefile`中，该任务首先打开`index.html`然后就开始监视源文件的改变了。

<span class="csscript"></span>

    task 'open', 'Open index.html', ->
      # First open, then watch
      spawn 'open', 'index.html'
      invoke 'watch'

你可以使用`option()`函数来给你的任务项定义参数，它接受一个短名、长名和描述三个参数。

<span class="csscript"></span>

    option '-o', '--output [DIR]', 'output dir'

    task 'build', 'Build lib/ from src/', ->
      # Now we have access to a `options` object
      coffee = spawn 'coffee', ['-c', '-o', options.output or 'lib', 'src']
      coffee.stderr.on 'data', (data) ->
        process.stderr.write data.toString()
      coffee.stdout.on 'data', (data) ->
        print data.toString()

如汝所见，任务项上下文可获一包含用户指定的所有参数的`options`对象。如若我等执行`cake`之令而不带任何参数，则所有任务项与参数列之。

Cake是一种自动化常见任务项（例如编译CoffeeScript）的有效的方式，避免了bash或者Makefiles的麻烦。 [Cake的源码](http://jashkenas.github.com/coffee-script/documentation/docs/cake.html)也值得一看，上面不但展示了CoffeeScript强大的表现力，旁边还有一份漂亮的由代码注释生成的文档。

##Server端的支持

对于静态的站点来说使用Cake就已经足够，但是对于动态的站点来说，我们就需要把CoffeeScript集成到请求/回应的生命周期中，对于那些比较流行的后端语言或者开发框架（例如[Rails](http://rubyonrails.org/)和[Django](https://www.djangoproject.com/)）来说已经存在各种各样的集成方案了。

Rails 3.1通过[Sprockets & the asset pipeline](https://github.com/sstephenson/sprockets)实现了对CoffeeScript的支持。把你的CoffeeScript放到`app/assets/javascripts`中，Rails就能够智能地按照请求需要对其进行预编译。可以使用特殊的注释来指示联合和打包JavaScript和CoffeeScript文件，这就意味着你可以通过一次请求获取程序的全部JavaScript。当发布到生成环境时，Rails会把编译后的文件输出出来，以保证能够缓存文件快速提供服务。

Rack服务器是其他可选方案之一，比如说37signal的[Pow](http://pow.cx/) 和Joshua Peek的[Nack](http://josh.github.com/nack/)都是。如果你的程序不需要Rails其他特性或者相关东西的话我力荐你使用它们，

Django通过特殊的模板标签也能[支持CoffeeScript](http://pypi.python.org/pypi/django-coffeescript/)。而且同时支持行内代码或者外联文件。

当需要编译CoffeeScript时，Ruby和Python在后台都是通过管道将其输出到Node和CoffeeScript库中，因此你在开发时需要事先安装好这些类库。如果你直接使用Node为你的站点开发后端程序，就非常容易集成CoffeeScript了，而且你还可以同时使用它来开发的前后台的代码。我们将在下一章介绍更多相关的东西，使用[Stitch](https://github.com/sstephenson/stitch)来为我们的客户端CoffeeScript提供服务。


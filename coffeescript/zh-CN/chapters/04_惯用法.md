<div class="back"><a href="index.html">&laquo; 返回目录</a></div>

#CoffeeScript惯用法

每个语言都有自己的惯用法和最佳实践，CoffeeScript也不例外。本章将为你揭示这些常规的东西，并且为你指出一些JavaScript到CoffeeScript的变化，以便你对这门语言有个感性的认识。

##Each

在JavaScript中我们既可以使用新加入的[`forEach()`](https://developer.mozilla.org/en/JavaScript/Reference/Global_Objects/array/foreach)也可以使用老的C语言风格的`for`循环来迭代一个数组。如果你打算使用一些在ECMAScript 5 提到的JavaScript新特性的话，我推荐你把这个 [shim](https://github.com/kriskowal/es5-shim) （薄层）来模拟这些特性以便支持老的浏览器。
    
    for (var i=0; i < array.length; i++)
      myFunction(array[i]);
      
    array.forEach(function(item, i){
      myFunction(item)
    });

尽管`forEach()`语法非常简洁易读，但有个缺点是在每次数组迭代时都需要调用回调函数，因此它比等价的`for`循环要慢。让我看一下这CoffeeScirpt中又是什么样子。

<span class="csscript"></span>
      
    myFunction(item) for item in array
    
这种语法易读简洁（我想你也这么认为），而且更棒的是这在背后会被编译为`for`循环。换句话说CoffeeScript的语法提供了`forEach()`的便捷，但是没有性能的损耗，也不需要shim的辅助。
    
##Map

与`forEach()`相同，ES5包含了一个比经典`for`循环在语法上更加简洁的函数，名为 [`map()`](https://developer.mozilla.org/en/JavaScript/Reference/Global_Objects/Array/map)。不过它与`forEach()`有一样需要注意的地方，其运行速度仍会受到函数调用的拖累。

    var result = []
    for (var i=0; i < array.length; i++)
      result.push(array[i].name)

    var result = array.map(function(item, i){
      return item.name;
    });

如我们在*语法*这一章所说，可以使用列表解析获得与`map()`同样的行为。注意最好使用括号把列表解析包裹起来，以便能够**完全地**确保列表解析返回你所想要的东西——映射后的数组。

<span class="csscript"></span>

    result = (item.name for item in array)

##筛选

ES5还提供了工具函数 [`filter()`](https://developer.mozilla.org/en/JavaScript/Reference/Global_Objects/array/filter) 来过滤数组：
    
    var result = []
    for (var i=0; i < array.length; i++)
      if (array[i].name == "test")
        result.push(array[i])

    result = array.filter(function(item, i){
      return item.name == "test"
    });

CoffeeScript的基础语法使用`when`关键字通过一个比较来过滤数组项。在背后会产生一个`for`循环，整个运行过程都包裹在一个匿名函数中，以防止作用域泄漏或变量冲突。

<span class="csscript"></span>

    result = (item for item in array when item.name is "test")

别忘了使用括号，否则`result`是数组的最后一项。  
CoffeeScript的列表解析是如此的灵活，允许你如下例这样做出强大地选择：

<span class="csscript"></span>

    passed = []
    failed = []
    (if score > 60 then passed else failed).push score for score in [49, 58, 76, 82, 88, 90]
    
    # Or
    passed = (score for score in scores when score > 60)
    
如果列表解析太长，你可以将它们分割成多行。

<span class="csscript"></span>

    passed = []
    failed = []
    for score in [49, 58, 76, 82, 88, 90]
      (if score > 60 then passed else failed).push score

##包含

通常使用`indexOf()`来测试一个数组中是否包含某个值。不过真让人惊讶, Internet Explorer并没有实现该函数，这还要做一些兼容。

    var included = (array.indexOf("test") != -1)

于此CoffeeScript有一个非常给力的替代方法，Python程序员一定很熟悉，名叫`in`。

<span class="csscript"></span>
    
    included = "test" in array

在背后，CoffeeScript使用的是 `Array.prototype.indexOf()`，必要的话提供shim方法来检测数组中是否有某个特定值，不幸的的是同样的`in`语法并不能在字符串中工作。我们退回去使用`indexOf()`函数，查看其返回值是否是负值：

<span class="csscript"></span>

    included = "a long test string".indexOf("test") isnt -1

或者更好一点，借助于位操作符我们就不用与`-1`进行比较了。

<span class="csscript"></span>
    
    string   = "a long test string"
    included = !!~ string.indexOf "test"
    
##属性迭代

在JavaScript中，你应该使用`in`操作符来迭代属性集，例如：

    var object = {one: 1, two: 2}
    for(var key in object) alert(key + " = " + object[key])
    
然而，如你在上一小节所知，CoffeeScript已把`in`关键字留给了数组用。作为替代，该操作符更名为`of`，可以像下面这样用：

<span class="csscript"></span>
    
    object = {one: 1, two: 2}
    alert("#{key} = #{value}") for key, value of object
    
如你所见，你可以同时指定属性名和属性值，非常方便。
    
##Min/Max

这个技巧虽然不是CoffeeScript的专利，但是我觉得它非常有用，值得一提。`Math.max` 和`Math.min`接受多个参数，因此你可以简单的地使用`...`来向它们传递数组，从中检索出最大值和最小值。

<span class="csscript"></span>

    Math.max [14, 35, -7, 46, 98]... # 98
    Math.min [14, 35, -7, 46, 98]... # -7
    
请注意，这个技巧对于超大的数组也会失败，因为浏览器对传递个函数的参数数量有限制。
    
##多个参数

在上面的`Math.max`示例中，我们使用`...`来结构数组作为多个参数传递给`max`方法。在背后，CoffeeScript将其转化为一个使用`appply()`的函数调用，以确保数组能够作为多个参数传递给`max`。在别的地方也可以使用这个特性，比方说代理函数：

<span class="csscript"></span>

    Log =
      log: ->
        console?.log(arguments...)
      
或者在参数继续传递下去之前，修改参数：

<span class="csscript"></span>

    Log =
      logPrefix: "(App)"

      log: (args...) ->
        args.unshift(@logPrefix) if @logPrefix
        console?.log(args...)
        
不过请记住，CoffeeScript会自动把函数的调用上下文设置为调用它的对象。在上例中，就是`console`对象，如果你想设置特殊的上下文，那你需要手动的调用`apply()`方法。

##And/or

CoffeeScript编程风格推荐使用`or`代替`||`，使用`and`代替`&&`。我知道为什么，因为前者看起来更直观。不过，这两种编程风格产生的结果都一样。

偏爱英语风格的代码的话，也可以使用`is`代替`==`，`isnt`代替`!=`。
    
<span class="csscript"></span>

    string = "migrating coconuts"
    string == string # true
    string is string # true
    
CoffeeScript还有另外一个非常好的扩展，Ruby程序员可将其看作像是`||=`这样的模式：
    
<span class="csscript"></span>

    hash or= {}
    
如果`hash`求值为`false`，则把它设置为一个空对象。在这里需要注意，表达式`0`、`""`和`null`都会被当作`false`。如果这并不是你想要的，那你应该使用CoffeeScript的存在操作符，这样只有`hash`是`undefined`或者`null`时才会触发。

<span class="csscript"></span>

    hash ?= {}

##解构赋值

解构赋值对任意深度嵌套的数组或对象都适用，方便从嵌套的属性中抽取值。

<span class="csscript"></span>

    someObject = { a: 'value for a', b: 'value for b' }
    { a, b } = someObject
    console.log "a is '#{a}', b is '#{b}'"
    
这在Node程序中引入模块时尤其有用：

<span class="csscript"></span>

    {join, resolve} = require('path')
    
    join('/Users', 'Alex')

##其他类库

既然所有的东西都会编译为JavaScript，那么使用其他类库与调用CoffeeScript类库的函数并没有什么差别。在CoffeeScript中使用 [jQuery](http://jquery.com)显得非常优雅，因为jQuery的API中有很多回调函数。

<span class="csscript"></span>

    # Use local alias
    $ = jQuery

    $ ->
      # DOMContentLoaded
      $(".el").click ->
        alert("Clicked!")
    
既然CoffeeScript编译输出的所有代码都被包裹在一个匿名函数中，因此我们可以使用一个局部变量`$`来代替`jQuery`。就算在jQuery的`no confict`模式或者`$`被重定义的情况下，我们的脚本能按预想的一样正常工作。

##私有变量

CoffeeScript中的`do`关键字能够让我们立即运行函数，这是一种非常有效的包装作用域和受保护变量的方式。在下面的例子中，我在被`do`立刻调用的匿名函数的上下文中定义了一个变量`classToType`。该匿名函数返回了另外一个匿名函数，它才是`type`最终的值。既然`classType`是在一个不保存引用的上下文中，因此在外部作用域中不可访问。

<span class="csscript"></span>

    # Execute function immediately
    type = do ->
      classToType = {}
      for name in "Boolean Number String Function Array Date RegExp Undefined Null".split(" ")
        classToType["[object " + name + "]"] = name.toLowerCase()
      
      # Return a function
      (obj) ->
        strType = Object::toString.call(obj)
        classToType[strType] or "object"

换句话说，`classToType`是完全私有的，并且在匿名函数执行完毕之后就不能在外面作用域中引用它了。这是一种非常好的包装作用域和变量的办法。

<div class="back"><a href="index.html">&laquo; 返回目录</a></div>

#糟粕

JavaScript是一个让人头疼的怪物，知道你不能用什么和应该用什么同样重要。孙武有言：“知己知彼，百战百胜”，这就是本章的目的，探索JavaScript不好的一面，揭示所有暗伏着准备偷袭毫无防备的开发者的怪兽。

在第一章介绍中说过，CoffeeScript的优秀不单单在于语法上的改进，还在于弥补JavaScript缺点的能力。然而，实际上CoffeeScript语句是直接翻译为JavaScript的，而不是运行在一个虚拟机或者解析器中，它不是解决所有JavaScript怪癖的银弹，还有一些地方需要你小心的处理。

首先，让我们讨论下这个语言解决的问题。

##JavaScript的子集

CoffeeScript的语法只包含了JavaScript的一个子集，众所周知的*精华部分*，因此这样就减少了需要处理的问题。让我们用`with`作为例子来看看。`with`被“看作有害的”好久了，应该避免使用它。`with`提供了一个频繁读写对象的快捷方式。例如，像下面这样写：

    dataObj.users.alex.email = "info@eribium.org";
    
你可以这样写：

    with(dataObj.users.alex) {
      email = "info@eribium.org";
    }
    
首先撇开我们不应该使用如此深的对象不说，这个语法非常漂亮。但必须除掉这一点。这会让JavaScript非常迷惑，它不知道你在`with`的上下文中到底要干嘛，使得它会强迫自己在任何变量查找时总是从这个特殊的对象开始。

这对性能损耗很大，而且意味着解析器必须关闭掉全部的JIT（运行时编译执行的技术）优化。还有，`with`语句并不能被像 [uglify-js](https://github.com/mishoo/UglifyJS)这样的压缩工具压缩掉。因此它在将来的JavaScript的版本中会被弃用和移除。综合考虑，避免使用它为妙，CoffeeScript更进一步，将其从自己的语法中拿掉了。换句话说，在CoffeeScript中使用`with`语句会报错。

##全局变量

默认情况下，你的JavaScript程序是在全局作用域下运行的，并且任何创建出来的变量默认的也会都在全局作用域中。如果你想创建一个局部变量的话，JavaScript要求使用`var`关键字来显式的指明。

    usersCount = 1;        // Global
    var groupsCount = 2;   // Global
                          
    (function(){              
      pagesCount = 3;      // Global
      var postsCount = 4;  // Local
    })()

这是一个比较奇怪的决定，既然大部分情况下你创建的是局部而不是全局变量，那么为什么要让它成为默认的呢？事实就是这样，开发者必须记住在定义变量之前加上`var`关键字，否则会造成冲突复写了彼此的变量，引起怪异的bug。

幸运的是，CoffeeScirpt通过彻底地剔除默认赋值给全局变量来解救你。换句话说，在CoffeeScript中`var`是保留关键字，一旦使用就会触发语法错误。默认地，会隐式地产生局部变量，而且不通过显示地给`window`的属性赋值的话，没法创建全局变量。

让我们看一个CoffeeScript变量赋值的例子：

<span class="csscript"></span>

    outerScope = true
    do ->
      innerScope = true
      
编译过来就是：

    var outerScope;
    outerScope = true;
    (function() {
      var innerScope;
      return innerScope = true;
    })();
    
注意CoffeeScript是如何自动的在上下文中第一次使用时自动的初始化变量（使用`var`）的。同时，我们无法覆盖外层作用域的变量，你仍然可以引用和访问它们。这你就要当心了，在你编译一个嵌套比较深的函数或者类时你就要当心不要意外的重用某个外层变量的名字。例如，这样我们就不小心在一个类函数中复写掉了`package`变量。

<span class="csscript"></span>

    package = require('./package')
    
    class Hem
      build: ->
        # Overwrites outer variable!
        package = @hemPackage.compile()
        
      hemPackage: ->
        package.create()
        
偶尔需要使用全局变量时，你需要将它们设置为`window`的属性才行：

<span class="csscript"></span>

      class window.Asset
        constructor: ->

通过保证显式的全局变量而不是隐式的，CoffeeScript移除了JavaScript程序中主要的bug源之一。

##分号

JavaScript并没有强制要求在源码中使用分号，因此可以省略分号。但是，在后台JavaScript还是需要分号的，因此当遇到由缺少分号造成的错误时解析器会自动的插入分号。换句话说，解析器首先对一个无分号的语句进行求值，如果失败的话，加上分号再试一次。

很不幸，这是一个非常糟糕的想法，这有可能完全改变代码的行为。看下面这个例子，这段JavaScript没有问题，对吗？

    function() {}
    (window.options || {}).property
    
不对，至少对解析器来说是这样，这会导致一个语法错误。如果以一个括号开头的话，解析器就不会插入一个分号。代码会被转化成一行：

    function() {}(window.options || {}).property

现在你可以看到问题在哪里，知道为什么解析器会抱怨了。当你写JavaScript的时候，你因该在语句后面加上分号。CoffeeScript通过在其语法中不使用分号来绕靠了这些麻烦。相反会在CoffeeScript编译为JavaScript时在把分号自动的插入到正确的位置上。

##保留字

某些关键字被JavaScript保留下来，以便将来新版本的JavaScript使用。比如说`const`、`enum`和`class`等。在你的JavaScript程序中使用这些保留字作为变量可能会导致不可预料的错误。有些浏览器可以很好的处理它们，而其他浏览器就会卡壳。CoffeeScript通过检测你使用的是否是保留字，在需要的时候将其转义，巧妙的绕过了这个问题。

例如，让我们把`class`保留字用作对象的属性，CoffeeScript代码可能是像下面这样：

<span class="csscript"></span>

    myObj = {
      delete: "I am a keyword!"
    }
    myObj.class = ->
    

CoffeeScript解析器会注意到你使用了保留字，而为你加上引号：

    var myObj;
    myObj = {
      "delete": "I am a keyword!"
    };
    myObj["class"] = function() {};
    
##相等比较

JavaScript的非严格等于比较会产生一些莫名其妙的行为，往往能引起bug。下面这个例子来自[JavaScript Garden's equality section](http://bonsaiden.github.com/JavaScript-Garden/#types.equality) ，对这个问题进行过深入的研究。

<span class="csscript"></span>

    ""           ==   "0"           // false
    0            ==   ""            // true
    0            ==   "0"           // true
    false        ==   "false"       // false
    false        ==   "0"           // true
    false        ==   undefined     // false
    false        ==   null          // false
    null         ==   undefined     // true
    " \t\r\n"    ==   0             // true

这些行为背后的原因是非严格等于会自动的强制类型转换。我相信你也同意这中方式是多么的模糊不清，而且会导致不可预料的结果和bug。

解决方案就是使用严格等于操作符来替换之，它由三个等号构成`===`。其行为与正常的等于操作符一样，但是不会进行任何的类型转换。推荐使用严格等于操作符，在需要的时候在显式地进行类型转换。
    
CoffeeScript通过简单的将非严格的比较替换为严格的来解决这个问题，也就是把所有的`==`比较转换为`===`。在CoffeeScript中无法使用非严格的比较，如果需要的话你应该事先显式地进行类型转换。

这并不意味着在你可以忽略CoffeeScript中所有的类型转换，尤其是在流程控制检测变量是否为“真值”时。空字符串、`null`、`undefined`和数字`0`都将转换为`false`。

<span class="csscript"></span>

    alert("Empty Array")  unless [].length
    alert("Empty String") unless ""
    alert("Number 0")     unless 0
    
如果你想显式的检查`null`和`undefined`的话，你可以使用CoffeeScript的存在操作符：

<span class="csscript"></span>

    alert("This is not called") unless ""?
    
在上例中，由于空字符串不等于`null`，所以`alert()`不会被调用。

##函数定义

在JavaScript有一点非常奇怪，函数可以在定义之前调用。例如，下面的代码能够正常运行，尽管`wem`实在被调用之后才被定义的：

    wem();
    function wem() {}

这与函数的作用域有关。在程序运行之前函数会被提升，这样的话函数在被定义的作用域的任何地方都可用，就算在源码中也可以在明确的定义之前调用它们。可问题在于，这中提升行为在浏览器上表现不一，例如：
    
    if (true) {
      function declaration() {
        return "first";
      }
    } else {
      function declaration() {
        return "second";
      }
    }
    declaration();
    
在像Firefox这类浏览器中，`declaration()`会返回`"first"`，而在其他类似于Chrome这类浏览器上会返回`"second"`，尽管看起来`else`语句永远不会被执行。

如果你想更加深入的了解函数定义式的话，你可以访问 [Juriy Zaytsev的指南](http://kangax.github.com/nfe/)，他对标准进行了深入的研究。但我只想说，它们有怪异的行为，在后面的代码中就会引起问题。综合考虑下来，使用函数表达式是避免这些问题的最佳方法。

    var wem = function(){};
    wem();

CoffeeScript通过彻底地移除函数定义式而只是用函数表达式来解决此问题。

##读取数值对象的属性

JavaScript解释器的一个瑕疵是数值对象的*点表示法*会被解释为一个浮点数，而不是一个属性的查找。比如，下面的JavaScript会产生一个语法错误：

    5.toString();
    
JavaScript解释器会在点后面查找数字，但是当它碰到的是`toString()`的话就会产生一个`Unexpected token`错误。既可以使用括号也可以多加一个点来解决这个问题。
    
    (5).toString();
    5..toString();
    
还好CoffeeScript解析器非常聪明，当你访问一个数字的属性时，自动地使用两个点标记来处理这个问题（如上面的例子所示）。

#没有修正的部分

CoffeeScript竭尽全力解决了JavaScript设计上的一些缺陷，但是也就到这一步了。如前所述，出于性能的考虑，CoffeeScript仅严格地限于静态的分析，在运行时不做任何的检查。CoffeeScript使用一个直接的源到源的编译器，其思想就是每句CoffeeScript代码都编译为等价的JavaScript语句。CoffeeScript并不针对JavaScript的关键字提供一层抽象，比方说`typeof`,因此JavaScript设计中的一些缺陷同样被带到了CoffeeScirpt中。

在上一部分中，我们看了一些CoffeeScirpt修复的JavaScript的设计缺陷，现在我们再来讨论一些CoffeeScript没能修正的JavaScript缺陷。

##使用eval

虽然CoffeeScript移除了一些JavaScript的缺陷，但是还有一些恶魔般必要的特性，你需要知道它们的短处。一个合理的例子就是`eval()`函数。毋庸置疑，这个函数自有其用武之地，但你需要知道它的阴暗面，而且需要尽量避免这些阴暗面。`eval()`函数会在局部作用域中执行JavaScript代码字符串，而且像`setTimeout()`和`setInterval()`也可以接受一个字符串作为第一个参数，对该参数求值。

然而，像`with`或`eval()`会逃脱编译器的跟踪，是一个主要的性能盲点。由于编译器不到运行的时候对其内部毫不知情，如嵌入的JavaScript代码一样，编译器不能对其做任何的优化。如果你给你的输入有误，`eval`很容易为你打开代码注入攻击之门。在你使用`eval`中的99%的时间中，应该有更好更安全的替代方式（比方说方括号）。

<span class="csscript"></span>

    # Don't do this
    model = eval(modelName)
    
    # Use square brackets instead
    model = window[modelName]
    
##使用typeof

`typeof`操作符是JavaScript最坑爹的设计，因为它完全就是鸡肋。事实上，它只有一个用途，就是检测一个值是否是`undefined`。

<span class="csscript"></span>

    typeof undefinedVar is "undefined"

对于其他所有的类型检测，`typeof`非常失败。依赖于浏览器和实例初始化方式不同会返回不一致的结果。于此CoffeeScript也无能为力，因为这语言用的是静态检查没有运行时的类型判断。你只能自食其力了。

为了说明问题所在，这是从[JavaScript Garden](http://bonsaiden.github.com/JavaScript-Garden/)拿来的表格，该表格展示了这个类型检测的关键字主要不稳定的地方。
  
    Value               Class      Type
    -------------------------------------
    "foo"               String     string
    new String("foo")   String     object
    1.2                 Number     number
    new Number(1.2)     Number     object
    true                Boolean    boolean
    new Boolean(true)   Boolean    object
    new Date()          Date       object
    new Error()         Error      object
    [1,2,3]             Array      object
    new Array(1, 2, 3)  Array      object
    new Function("")    Function   function
    /abc/g              RegExp     object
    new RegExp("meow")  RegExp     object
    {}                  Object     object
    new Object()        Object     object
    
你会发现，使用引号还是`String`类来定义一个字符串会影响`typeof`的结果。`typeof`应该都返回`"string"`才符合逻辑，但是对于后者它返回的却是`"object"`。很不幸，这样的不一致性只会让事情变得更糟糕。

那在JavaScript中我们如何做类型检查呢？还好有`Object.prototype.toString()`来解围。如果我们以某个特别的对象为上下文来调用该函数，它会返回正确的类型。我们所需要做的就是手动处理其返回的字符串，这样最终我们就能获得`typeof`返回的那种字符串。下面是从jQuery的`$.type`移植过来的例子：

<span class="csscript"></span>

    type = do ->
      classToType = {}
      for name in "Boolean Number String Function Array Date RegExp Undefined Null".split(" ")
        classToType["[object " + name + "]"] = name.toLowerCase()
      
      (obj) ->
        strType = Object::toString.call(obj)
        classToType[strType] or "object"
    
    # Returns the sort of types we'd expect:
    type("")         # "string"
    type(new String) # "string"
    type([])         # "array"
    type(/\d/)       # "regexp"
    type(new Date)   # "date"
    type(true)       # "boolean"
    type(null)       # "null"
    type({})         # "object"
    
如果你想检查某个变量是否被定义，你仍然需要使用`typeof`，否则你会得到一个`ReferenceError`的错误。

<span class="csscript"></span>

    if typeof aVar isnt "undefined"
      objectType = type(aVar)
      
或者使用更加简洁的存在操作符：

    objectType = type(aVar?)
    
作为类型检测的替代，你通常还可以使用鸭子类型检测结合CoffeeScript的存在操作符来避免确定一个对象的类型。例如，假设我们将向一个数组中加入一个值。我们可以这么说，既然这个`类数组`对象实现了`push()`方法，我们应该把它当作一个数组：

<span class="csscript"></span>

    anArray?.push? aValue
    
如果`anArray`是一个对象而不是一个数组，那么存在操作符会保证`push()`绝不会被调用。
    
##使用instanceof

JavaScript的`instanceof`关键字几乎就和`typeof`一样不给力。理想的`instanceof`将比较两个对象的构造器，看其中一个是否另外一个的实例而返回真假值。实际上`instanceof`只在比较自定义的对象上工作正常。如果用来比较内置类型时，就像`typeof`一样废。

<span class="csscript"></span>

    new String("foo") instanceof String # true
    "foo" instanceof String             # false
    
而且，`instanceof`当比较不用浏览器里不同frame中的对象时也不能正常工作。实际上，`instanceof`对于自定义的对象才会返回正确的值，比方说CoffeeScript的类型。

<span class="csscript"></span>

    class Parent
    class Child extends Parent
    
    child = new Child
    child instanceof Child  # true
    child instanceof Parent # true
    
确定你只在比较你自己的对象时使用它，或者坚决不用更好。

##使用delete

`delete`关键字只能用来移除对象内部的属性。

<span class="csscript"></span>

    anObject = {one: 1, two: 2}
    delete anObject.one
    anObject.hasOwnProperty("one") # false

用于其他地方，比方说删除一个变量或者函数，就完全不行。

<span class="csscript"></span>

    aVar = 1
    delete aVar
    typeof Var # "integer"

这种行为非常古怪，但是你已经知道了。如果你想移除一个变量的引用，将其赋值为`null`即可。

<span class="csscript"></span>

    aVar = 1
    aVar = null

##使用parseInt

如果你给JavaScript的`parseInt()`函数传递一个字符串而没有指明基数的话它会返回一个让你意外的值，例如：

    # Returns 8, not 10!
    parseInt('010') is 8
    
给该函数传递一个基数让它能够正常工作：

    # Use base 10 for the correct result
    parseInt('010', 10) is 10

这些用法CoffeeScript并不能为你做，每次当你使用`parseInt()`函数的时候别忘了给它传递一个基数。
    
##Strict模式

Strict模式是ECMAScript5的一个新特性，它能够让你在一种*严格*的上下文中运行JavaScript程序或者函数。比起普通上下文，严格上下文会抛出更多的异常和警告，当开发者不遵循最佳实践，写无优化的代码或者犯常见错误时，它能给他们一些提示。换句话说，严格模式能够减少bug，提高安全性，提升性能以及消除比较难于驾驭的语言特性。这难道不好么？

到目前为止，下面这些浏览器支持Strict模式：

* Chrome >= 13.0
* Safari >= 5.0
* Opera >= 12.0
* Firefox >= 4.0
* IE >= 10.0

话说回来，严格模式能完全兼容较老的浏览器。使用它的程序在严格或者普通模式下都能正常工作。

###Strict模式的不同

严格模式的大部分改变都是与Javascript语法相关的：

* 重复属性和重复函数参数会报错
* 不合理使用`delete`会报错
* 访问`arguments.caller` 或`arguments.callee` 报错（与性能有关）
* 使用`with`的话会抛出语法错误
* `undefined`等之类的确定的变量不再可写
* 引入更多的保留关键字，比方说 `implements`、`interface`、`let`、`package`、`private`、`protected`、`public`、`static`和`yield`

而且，严格模式还改变了一些运行时的行为：

* 全局变量必须显式申明（必须使用`var`）。全局`this`的值是`undefined`
* `eval`无法在局部作用域中引入新变量
* 函数必须在它们被使用前定义（先前的函数可以[定义在任何地方](http://whereswalden.com/2011/01/24/new-es5-strict-mode-requirement-function-statements-not-at-top-level-of-a-program-or-function-are-prohibited/)）
* `arguments`不可变

CoffeeScript已经遵循了许多严格模式的要求，比方说在定义变量时一定会用`var`，但是在你的CoffeeScript程序中开启严格模式还是很有用的。实际上，在未来的版的CoffeeScript中在编译时会检查程序是否遵循严格模式。

###使用Strict模式

为了开启严格的检查，你只需要在你的脚本或者函数中添加像下面这样的字符串即可：

<span class="csscript"></span>
    
    ->
      "use strict"
    
      # ... your code ...
      
就是这样，直接使用`'use strict'`字符串。没有比这更简单的了，而且它完全向后兼容。让我们实际试试严格模式。下面的代码在严格模式下会引起一个语法错误，而在普通模式下就没问题：

<span class="csscript"></span>

    do ->
      "use strict"
      console.log(arguments.callee)
      
在Strict模式下，对`arguments.caller`和`arguments.callee`的访问已经被移除了，因为它们是性能损耗的要点。现在只要使用它们就会抛出语法错误。

在使用严格模式的时候，有个地方你需要额外小心。即使用`this`来创建全局变量。下面的例子在严格模式下会抛出一个`TypeError`的错误，但是在普通模式下能正常运行，产生一个全局变量：

<span class="csscript"></span>

    do ->
      "use strict"
      class @Spine
      
这背后的原因是不一致性，即在严格模式下`this`是`undefined`的，而在普通模式下它指向`window`对象。解决办法就是像这样显式的在`window`对象上设置全局变量。

<span class="csscript"></span>

    do ->
      "use strict"
      class window.Spine
      
虽然我推荐使用严格模式，但是并不值得这么做，一是严格模式并不会引入JavaScript中还没有完全准备好的特性，再者由于VM（虚拟机）在运行时会进行更多的检查从而让你的代码变慢。你应该在开发时使用严格模式，而发布时去掉它。

##JavaScript Lint

[JavaScript Lint](http://www.javascriptlint.com/)是一个JavaScript代码质量检测工具，通过它运行你的代码是一种提高代码质量和最佳实践的非常好的方式。该项目基于另外一个类似的名为[JSLint](http://www.jslint.com)的工具。到JSLint的网站上查看一个[非常棒的列表](http://www.jslint.com/lint.html)，该列表包含了所有它会检查的问题。包括全局变量、丢失的括号以及非严格相等比较。

好消息是CoffeeScript已经对编译输出进行过`lints`检查，因此CoffeeScript产生的JavaScript已经是与JavaScript Lint兼容的代码了。事实上，`coffee`命令工具支持`--lint`参数：

    coffee --lint index.coffee
      index.coffee:	0 error(s), 0 warning(s)
